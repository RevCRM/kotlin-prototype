
buildscript {
    ext {
        springBootVersion = '2.1.3.RELEASE'
        kotlinVersion = '1.3.21'
    }
    ext['kotlin.version'] = '1.3.21'  // https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/235
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}"
    }
}

plugins {
    id "com.moowork.node" version "1.2.0"
    id "org.jmailen.kotlinter" version "1.21.0"
    id "name.remal.check-updates" version "1.0.94"
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
}

subprojects {
    group = 'org.revcrm'
    version = '1.0'

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.jetbrains.kotlin.plugin.noarg'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jmailen.kotlinter'
    apply plugin: 'com.moowork.node'
    apply plugin: 'name.remal.check-updates'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    sourceCompatibility = 1.8

    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
        implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        implementation 'xyz.morphia.morphia:core:1.4.0'
        implementation 'org.mongodb:mongo-java-driver:3.9.1'
        implementation 'org.hibernate:hibernate-validator:6.0.14.Final'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-engine"
        testImplementation "io.mockk:mockk:1.8.10"
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }

    noArg {
        annotation("xyz.morphia.annotations.Entity")
        annotation("org.revcrm.annotations.EmbeddedEntity")
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = "1.8"
        }
    }

    test {
        useJUnitPlatform()
        reports.html.enabled = false
    }

    node {
        version = '10.14.1'
        npmVersion = '6.4.1'
        download = true
    }
}
